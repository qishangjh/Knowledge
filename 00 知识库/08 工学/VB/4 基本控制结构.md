---
课程名称: 
授课教师: 
tags: []
---

# 4 基本控制结构

---

## 4.1 算法
解决某个问题或实现某项功能的方法和步骤，是对特定问题求解步骤的一种描述。

具有5个特性:  
1）确定性：每个步骤都是确定的，没有歧义性  
2）可行性：每个步骤都是计算机能实现的  
3）有穷性：所包含的步骤是有限的  
4）输入性：0个或多个输入  
5）输出性：1个或多个输出

---

例：从键盘上输入三角形3边，求三角形的面积  
步骤如下：  
Step1: 从键盘输入三个整数，用a,b,c存储  
Step2: 判断a,b,c能否构成三角形（两边之和大于第三边）  
Step3: 如果符合，求s=(a+b+c)/2，再求面积area=  
Step4: 输出三角形面积

![](file:///C:\Users\gjh99\AppData\Local\Temp\ksohtml14724\wps20.png)

---

## 4.2 程序流程图

### 一、程序流程图符号

![](file:///C:\Users\gjh99\AppData\Local\Temp\ksohtml14724\wps21.png)

输入/输出框

---

用流程图表示求三角形面积的步骤  
![[Pasted image 20250526001615.png]]

---

## 4.3 顺序结构

### 4.3.1 输入和输出
#### 1.数据输入
1）通过文本框等控件，得到的值是字符串型  
2）通过InputBox函数，弹出一个输入框

---

InputBox函数的语法格式:  

```vb
InputBox(提示,[标题],[默认值]）
```

如：  

```vb
m = InputBox("输入分数(0到100之间):", "数据输入", "80")
```

---
#### 2. 数据输出
##### 1）通过控件，如label控件
##### 2）通过VB提供的方法和函数
###### （1）Print 方法
  语法格式：  

```vb
 [对象.] print  spc(n)|Tab(n) [输出项] [,|;]
```

Spc(n):n个空格  
Tab(n):光标定位在第n列上  
分号：插入点定位在上一个输出字符后  
逗号：插入点定位在制表符上

```vb
   Print
  Print Tab(15); "变量必须先声明后使用，这是为什么？"
  Print Tab(15); "素数是" & "5", "7", "11"
  Print Tab(15); "素数是" & "5"; "7"; "11"
```

---
###### (2) MsgBox函数
语法格式：

```vb
变量=MsgBox(提示,按钮,标题)
```

---
MsgBox中标志和按钮值列表

| 内部常数               | 按钮值 | 说明                       |
| ------------------ | --- | ------------------------ |
| VbOKOnly           | 0   | 只显示OK按钮                  |
| VbOKCancel         | 1   | 显示OK和CANCEL两个按钮          |
| Vbabortretryignore | 2   | 显示Abort、Retry和Ignore三个按钮 |
| VbyesNoCancel      | 3   | 显示yes、No、Cancel三个按钮      |
| VbYesNo            | 4   | 显示Yes和No两个按钮             |
| VbRetryCancel      | 5   | 显示Retry和Cancel两个按钮       |
| VbCritical         | 16  | 显示CriticalMessage图标      |
| VbQuestion         | 32  | 显示WarningQuery图标         |
| VbExclamation      | 48  | 显示WarningQuery图标         |
| VbInformation      | 64  | 显示InformationMessage图标   |

---

MsgBox函数返回值列表

| 内部常数     | 返回值 | 说明     |
| -------- | --- | ------ |
| Vbok     | 1   | Ok     |
| Vbcancel | 2   | Cancel |
| Vbabort  | 3   | Abort  |
| Vbretry  | 4   | Retry  |
| Vbignore | 5   | Ignore |
| Vbyes    | 6   | Yes    |
| Vbno     | 7   | no     |

---
### 4.3.2 赋值语句

```vb
[LET] 变量名=表达式
```

注意：  
1）当表达式的类型与变量的类型不一致时，强制转换成左边的精度  
  number%=10/3  
2）赋值号与关系运算符的等于一样，VB自动判断  
    dim a as integer  
    a=3  
   if(a=5)  
3）赋值号左边只能是变量，不能是常量或表达式  
   a+3=10    错误  
4）转换规则：如果等号右边是数字字符串，左边变量是数值型，直接转换成变量类型。  
  number%="123"     等价于    number%=val("123")

NaN. 赋值号右边的任何非字符型的值，赋值给左侧的字符型变量时，会自动转换为字符型。

```vb
Dim number As Integer
Dim zhouchang As Single
Dim mianji As Single
Const PI As Single = 3.1415926
number = Val(InputBox("请输入一个整数"))
mianji = PI * number * number
zhouchang = 2 * PI * number
Print zhouchang; mianji
```

---

## 4.4 选择结构
### 4.4.1 二路分支
#### 1. If…Then…End if语句

```vb
      If<表达式>  Then
          语句块
      End if
 或
      If<表达式> Then  <语句>
```

1）条件表达式是用关系运算符构成的关系表达式或由逻辑运算符构成的逻辑表达式，结果为True或False。  
2）条件表达式是由算术运算符构成的算术表达式，其结果为数值，将零看作False，将非零看作True。

---

例：从键盘输入两个整数x和y，然后升序输出。

```vb
  Dim x, y, t As Integer
  x = Val(InputBox("请输入一个x值"))
  y = Val(InputBox("请输入一个y值"))
  Print "交换前：", x, y
  If x > y Then t = x: x = y: y = t
  Print "交换后：", x, y
```

从键盘输入三个整数，按照从小到大的顺序输出。

---

已知百分制成绩mark，显示对应的五分制成绩

| 五分制(grade) | 百分制成线（mark)            |
| ---------- | ---------------------- |
| 优秀         | Mark>=90 and mark<=100 |
| 良好         | Mark>=80 and mark<90   |
| 中          | Mark>=70 and mark<80   |
| 及格         | Mark>=60 and mark<70   |
| 不及格        | Mark<60                |

---

```vb
Dim mark As Single
Dim grade As String
mark = Val(InputBox("请输入一个百分数"))
If mark >= 90 And mark <= 100 Then
    grade = "优秀"
 End If
  If mark >= 80 And mark < 90 Then
    grade = "良好"
 End If
  If mark >= 70 And mark < 80 Then
    grade = "中"
 End If
  If mark >= 60 And mark < 70 Then
    grade = "及格"
 End If
  If mark < 60 Then
    grade = "不及格"
 End If
 Print grade
```

---

#### 2. if…Then…Else…End if
语法格式为：

```vb
If 表达式 Then
   语句体1
else
  语句体2
end if
```

---

例：已知三角形的3边，求三角形面积

```vb
Private Sub Form_Click()
  Dim x, y, t As Integer
  Dim s, area As Single
  x = Val(InputBox("请输入一个x值"))
  y = Val(InputBox("请输入一个y值"))
  z = Val(InputBox("请输入一个z值"))
  If x < y + z And y < x + z And z < x + y Then
     s = (x + y + z) / 2
     area = Sqr(s * (s - x) * (s - y) * (s - z))
     Print area
  Else
     Print "输入的3边不符合三角形定义的要求"
  End If
End Sub
```

---

#### 3. If…Then…ElseIf…End If

```vb
语法格式：
If  表达式1 Then
   语句体1
elseif  表达式2  Then
   语句体2
[elseif  表达式3  Then
   语句体3]
… …
[else
    语句体n]
End if
```

---

例：用If…Then…ElseIf…End If语句实现百分制成线mark到五分制成绩的转换。

```vb
Private Sub Form_Click()
  Dim mark As Single
  Dim grade As String
  mark = Val(InputBox("请输入百分制成绩"))
  If mark >= 90 Then
     grade = "优"
  ElseIf mark >= 80 Then
     grade = "良"
  ElseIf mark >= 70 Then
     grade = "中"
  ElseIf mark >= 60 Then
     grade = "及格"
  Else
     grade = "不及格"
  End If
  Print grade
End Sub
```

---

#### 4. IIF函数

语法结构为:

```vb
IIf(<条件表达式>,<真部分>,<假部分>)
```

当条件表达式的值为真时，函数返回值为真部分，否则返回假部分。

---

例：输入x,计算y的值，其中：

```vb
Private Sub Form_Click()
  Dim x, y As Single
  x = Val(InputBox("请输入x值"))
  y = IIf(x >= 0, 1 + x, 2 * x)
  Print y
End Sub
```

---

#### 5. If语句的嵌套

```vb
If <表达式1> Then
      If<表达式2>  Then
     ……
      End if
     ……
End if
```

---

### 4.4.2 多路分支
语法格式为：

```vb
Select Case  变量或表达式
    Case 表达式1
        <语句块1>
    Case 表达式2
        <语句块2>
        ：
        ：
        ：
     Case Else
         语句块n
End Select
```

---

说明：  
1）Select Case后面的表达式，可以是算术表达或字符表达式  
2）表达式i与表达式类型必须相同，可以是下列几种形式之一  
    用逗号分隔的：把所有可能值列举出来，满足其一即执行对应的语句。如：10，20，15  
    表达式to表达式：表示一个范围  
    Is关系运算表达式：关系表达式只能是简单条件，不能是用逻辑运算符连接形成的复合条件。如：Is<500

---

例：设计一个窗体，通过给InputBox输入一个颜色值（红、蓝、绿），根据输入值设置窗体的背景色。

```vb
Private Sub Form_Click()
  Dim strColor As String
  strColor = InputBox("输入颜色的名称(red、blue或green)")
  strColor = LCase(strColor)
  Select Case strColor
      Case "red"
         Form1.BackColor = RGB(255, 0, 0)
      Case "green"
         Form1.BackColor = RGB(0, 255, 0)
      Case "blue"
         Form1.BackColor = RGB(0, 0, 255)
      Case Else
         MsgBox "请选择要求的颜色"
  End Select
```

---

例：用select…case语句完成成绩百分制到五分制的转化

```vb
Private Sub Form_Click()
 Dim mark As Single
 mark = Val(InputBox("请输入一个百分制成绩"))
 Select Case mark
     Case 90 To 100
        Print "优"
     Case 80 To 90
        Print "良"
    Case 70 To 80
       Print "中"
    Case 60 To 70
      Print "及格"
    Case Else
      Print "不及格"
 End Select
End Sub
```

---

例：设有如下函数，实现输入x值后，输出相应y值  
![[Pasted image 20250526002515.png]]

---

```vb
Private Sub Form_Click()
  Dim x, y As Single
  x = Val(InputBox("请输入x值"))
  Select Case x
     Case Is > 3
        y = x + 3
     Case 1 To 3
        y = x * x
     Case Is > 0
        y = x
     Case Else
       y = 0
  End Select
  Print y
End Sub
```

---

## 4.5 循环结构
### 1. for…Next语句
语法格式：

```vb
For  循环变量 = 初值 To 终止值 [Step 步长值]
        循环体
Next  循环变量
```

---

例：计算1~100之间的自然数的和

```vb
Private Sub Form_Click()
   Dim i%, sum%
   sum = 0
   For i = 1 To 100
      sum = sum + i
   Next i
   Print sum
End Sub
```

思考：求1~100之间的奇数和

---

例：求5!

```vb
Private Sub Form_Click()
   Dim i%, s%
   s = 1
   For i = 1 To 5
      s = s * i
   Next i
   Print s
End Sub
```

思考：求任意一个整数n！

---

### 2. Do……Loop结构
语法格式：

```vb
DO [{While|Until} condition]
    [Statements]
    [Exit Do]
    [Statements]
Loop
'或者
DO
    [Statements]
    [Exit Do]
    [Statements]
Loop [{While|Until} condition]
```

---

四种形式：  

```vb
Do While 循环条件
    ……
    [Exit Do]
   ……
Loop
```

```vb
Do
   ……
    [Exit Do]
   ……
Lopp While 循环条件  
```

```vb
DO Until 循环条件  
……  
 [Exit Do]  
 ……  
LOoP 
``` 

```vb
DO  
    ……  
    [Exit Do]  
   ……  
LooP Until 循环条件
```

---

例：用do…loop循环实现1~100之间的自然的和

```vb
Private Sub Form_Click()
   Dim n%, sum%
   n = 1: sum = 0
   Do While n <= 100
      sum = sum + n
      n = n + 1
   Loop
   Print "sum="; sum
End Sub
Private Sub Form_Click()
   Dim n%, sum%
   n = 1: sum = 0
   Do Until n > 100
      sum = sum + n
      n = n + 1
   Loop
   Print "sum="; sum
End Sub
```

其他两种形式自己试一下

---

例：从键盘上输入一个正整数，将其逆序输出

```vb
Private Sub Form_Click()  
   Dim number, a As Integer
   number = Val(InputBox("请输入一个正整数"))
   Print "输入数为：" & number
   Do
      a = number Mod 10
      Print a;
      number = number \ 10
   Loop While number <> 0
End Sub
```

---

### 4.5.2 循环的嵌套

```vb
For i=…
   ……
  for j=…
     …
  next j
  ……
Next i
```

```vb
For i=…
    ……
     do while/until….
       ……
     loop
     …….
Next  i
```

```vb
Do while/until…
    …
    do while/until…
         …
    loop
      ……
loop
```

```vb
 do while…
       …
      for j=…
         …
      next j
     …
loop
```

---

例：打印九九乘法表

```vb
Private Sub Form_Click()
   Dim se As String
   Dim i, j As Integer
   For i = 1 To 9
       For j = 1 To 9
           se = i & "*" & j & "=" & i * j
           Print Tab((j - 1) * 9 + 1); se;
       Next j
       Print
   Next i
End Sub
```

![[4 基本控制结构 2025-05-24 20.39.19.excalidraw]]  
思考：计算1!+2!+3!+…+10!

---

## 4.6 循环结构应用举例
### 4.6.1 累加、累乘算法
例：计算1~100中所有5或7的倍数的和

```vb
Private Sub Form_Click()
   Dim i, sum As Integer
   sum = 0
   For i = 1 To 100
       If i Mod 5 = 0 Or i Mod 7 = 0 Then
          sum = sum + i
       End If
   Next i
   Print sum
End Sub
```

---

### 4.6.2 枚举算法
例：编写程序实现用1元人民币换成1分、2分、5分的硬币共50枚的各种兑换方案  
![[Pasted image 20250526001916.png]]

---

```vb
Private Sub Form_Click()
   Dim i, j, k As Integer
   For i = 0 To 50
      For j = 0 To 50
         For k = 0 To 50
             If i + j + k = 50 And i + 2 * j + 5 * k = 100 Then
                 Print i, j, k
            End If
         Next k
      Next j
  Next i
End Sub
```

---

```vb
Private Sub Form_Click()
   Dim i, j, k As Integer
   For i = 0 To 50
      For j = 0 To 50
         k = 50 - i - j
             If i + 2 * j + 5 * k = 100 Then
                 Print i, j, k
            End If
      Next j
  Next i
End Sub
```

---

### 4.6.3 递推算法
例：输出Fibonacci数列的前20项

```vb
Private Sub Form_Click()
   Dim f1, f2, f, i As Integer
   f1 = 1: f2 = 1
   Print f1; f2; Space(2);
   For i = 3 To 20
      f = f1 + f2
      Print f & Space(2);
      f1 = f2
      f2 = f
   Next i
End Sub
```

---

例：输入一个整数，判断其是否为素数。

```vb
Private Sub Form_Click()
  Dim flag As Boolean
  Dim number, i As Integer
  number = Val(InputBox("请输入一个整数"))
  flag = True
  For i = 2 To number - 1
     If number Mod i = 0 Then
        flag = False
        Exit For
     End If
  Next i
  If flag = True Then
      Print number & "是素数"
  Else
      Print number & "不是素数"
  End If
End Sub
```
